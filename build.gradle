import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
}

group 'edu.sdsu.its'
version '1.0'
final def versionCode = "${project.version}"
final def buildCode = "${buildNumber()}"

apply plugin: 'java'
apply plugin: 'war'


repositories {
    mavenCentral()
    jcenter()
}

war {
    archiveName 'ms-monitor.war'
}

lombok {
    sha256 = "" // skip verify Lombok task
}

dependencies {
    // Logging
    compile 'log4j:log4j:1.2.17'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    // WebServer
    compile 'javax.ws.rs:javax.ws.rs-api:2.0'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.22.1'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // DB
    compile 'mysql:mysql-connector-java:5.1.38'

    // Web Requests (Outbound)
    compile 'com.google.code.gson:gson:2.5'
    compile 'org.apache.httpcomponents:httpclient:4.4'

    // Twig
    compile "org.jtwig:jtwig-web:5.85.3.RELEASE"

    // Vault
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'

    // Email and Reporting
    compile 'org.apache.commons:commons-email:1.4'

    // Scheduler
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.3'

    // Password Hashing
    compile group: 'org.jasypt', name: 'jasypt', version: '1.9.2'

    // Commons
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'com.google.guava', name: 'guava', version: '22.0'

    // Unit Tests
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

static def buildNumber() {
    return Long.toString(System.currentTimeMillis(), 36).toUpperCase()
}

static def getHost() {
    return InetAddress.getLocalHost().getHostName()
}

def makeBuildInfoFile(versionCode, buildCode) {
    println 'Saving Build Info to src/main/resources/build_info.properties'
    def configFile = new File('src/main/resources/build_info.properties')
    def config = new Properties()
    config.setProperty("version", versionCode as String)
    config.setProperty("build", buildCode as String)
    config.setProperty("time", "${ZonedDateTime.now().format(DateTimeFormatter.ISO_INSTANT)}")
    config.setProperty("host", getHost())
    config.setProperty("user", "${System.getProperty("user.name")}")
    config.store(new FileOutputStream(configFile), null)
}

clean.doLast {
    // Delete Build Info File
    new File('src/main/resources/build_info.properties').delete()
}

processResources.doFirst {
    makeBuildInfoFile(versionCode, buildCode)
}